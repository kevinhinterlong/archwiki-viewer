plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.4.2'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    mavenCentral()
}

android {
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "archwiki-viewer-v${variant.versionName}.apk"
        }
    }

    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "com.jtmcn.archwiki.viewer"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 14
        versionName "1.0.13"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'archwiki-viewer'
            keyAlias 'archwiki-viewer'
            keyPassword 'archwiki-viewer'
        }
        if (file('upload.keystore').exists()) {
            upload {
                storeFile file('upload.keystore')
                storePassword System.getenv('ARCHWIKI_VIEWER_UPLOAD_STORE_PASSWORD')
                keyAlias 'awvalias'
                keyPassword System.getenv('ARCHWIKI_VIEWER_UPLOAD_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (file('upload.keystore').exists()) {
                signingConfig signingConfigs.upload
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.squareup.okhttp3:okhttp:3.11.0'

    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

play {
    serviceAccountCredentials = file("upload.json")
    track = "beta"
}
